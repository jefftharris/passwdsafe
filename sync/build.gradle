/*
 * Copyright (Â©) 2017-2024 Jeff Harris <jefftharris@gmail.com>
 * All rights reserved. Use of the code is allowed under the
 * Artistic License 2.0 terms, as specified in the LICENSE file
 * distributed with this code, or available from
 * http://www.opensource.org/licenses/artistic-license-2.0.php
 */
apply plugin: 'com.android.application'
apply plugin: 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'

android {
    namespace 'com.jefftharris.passwdsafe.sync'
    compileSdk 35
    defaultConfig {
        applicationId "com.jefftharris.passwdsafe.sync"
        minSdkVersion 26
        resourceConfigurations += ['de', 'fr', 'nb']
        targetSdkVersion 34
        // <major><minor><bugfix><beta/extra>
        versionCode 3110000
        versionName "3.11.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildFeatures {
        buildConfig true
    }
    buildTypes {
        debug {
            debuggable true
            // Uncomment below option to minify during debug development
            // minifyEnabled true
            proguardFiles += getDefaultProguardFile(
                    'proguard-android-optimize.txt')
            proguardFiles += 'proguard-rules.pro'
            proguardFiles += 'proguard-google-api-client.pro'
        }
        release {
            minifyEnabled true
            proguardFiles += getDefaultProguardFile(
                    'proguard-android-optimize.txt')
            proguardFiles += 'proguard-rules.pro'
            proguardFiles += 'proguard-google-api-client.pro'
            // Uncomment below to run minified release build in emulator
            // signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':lib')
    implementation 'androidx.lifecycle:lifecycle-livedata:2.8.7'
    implementation('androidx.work:work-runtime:2.10.0') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    implementation 'com.google.android.gms:play-services-auth:21.3.0'
    // Google Drive
    implementation('com.google.api-client:google-api-client-android:2.2.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev20230815-2.0.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    // Dropbox
    implementation 'com.dropbox.core:dropbox-core-sdk:7.0.0'
    implementation 'com.dropbox.core:dropbox-android-sdk:7.0.0'
    // Box
    implementation('com.box:box-android-sdk:5.0.0') {
        exclude group: 'com.google.guava'
    }
    // OneDrive
    implementation ('com.microsoft.identity.client:msal:5.8.2') {
        exclude group: 'com.microsoft.device.display'
    }
    implementation 'com.microsoft.graph:microsoft-graph:6.25.1'
}

secrets {
    propertiesFileName = "secrets.properties"
    defaultPropertiesFileName = "secrets-defaults.properties"
}
