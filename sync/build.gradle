/*
 * Copyright (Â©) 2017-2024 Jeff Harris <jefftharris@gmail.com>
 * All rights reserved. Use of the code is allowed under the
 * Artistic License 2.0 terms, as specified in the LICENSE file
 * distributed with this code, or available from
 * http://www.opensource.org/licenses/artistic-license-2.0.php
 */
apply plugin: 'com.android.application'
apply plugin: 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'

android {
    namespace 'com.jefftharris.passwdsafe.sync'
    compileSdk 34
    defaultConfig {
        applicationId "com.jefftharris.passwdsafe.sync"
        minSdkVersion 21
        resourceConfigurations += ['de', 'fr', 'nb']
        targetSdkVersion 34
        // <major><minor><bugfix><beta/extra>
        versionCode 3100000
        versionName "3.10.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildFeatures {
        buildConfig true
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles += getDefaultProguardFile(
                    'proguard-android-optimize.txt')
            proguardFiles += 'proguard-rules.pro'
            proguardFiles += 'proguard-google-api-client.pro'
        }
        release {
            minifyEnabled true
            proguardFiles += getDefaultProguardFile(
                    'proguard-android-optimize.txt')
            proguardFiles += 'proguard-rules.pro'
            proguardFiles += 'proguard-google-api-client.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':lib')
    implementation project(':lib-owncloud')
    implementation 'androidx.lifecycle:lifecycle-livedata:2.7.0'
    implementation('androidx.work:work-runtime:2.9.0') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    // Google Drive
    implementation('com.google.api-client:google-api-client-android:2.2.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev20230815-2.0.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    // Dropbox
    implementation('com.dropbox.core:dropbox-core-sdk:5.4.5') {
        exclude group: 'com.google.guava'
    }
    // Box
    implementation('com.box:box-android-sdk:5.0.0') {
        exclude group: 'com.google.guava'
    }
    // OneDrive
    implementation 'com.microsoft.graph:msgraph-sdk-android:1.7.0'
    implementation('com.microsoft.identity.client:msal:0.2.2') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.google.com.gson'
    }
    // Misc dependencies
    implementation 'com.google.code.gson:gson:2.10.1'
}

secrets {
    propertiesFileName = "secrets.properties"
}
