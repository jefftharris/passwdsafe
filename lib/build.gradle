/*
 * Copyright (Â©) 2016 Jeff Harris <jefftharris@gmail.com>
 * All rights reserved. Use of the code is allowed under the
 * Artistic License 2.0 terms, as specified in the LICENSE file
 * distributed with this code, or available from
 * http://www.opensource.org/licenses/artistic-license-2.0.php
 */
import java.text.SimpleDateFormat

apply plugin: 'com.android.library'

static def hgId() {
    return 'hg id'.execute().text.trim()
}

static def buildTime() {
    def df = new SimpleDateFormat("MM/dd/yyyy HH:mm:ss Z")
    return df.format(new Date())
}

android {
    namespace 'com.jefftharris.passwdsafe.lib'
    compileSdkVersion 33
    buildToolsVersion '30.0.3'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33
        vectorDrawables.useSupportLibrary = true
        buildConfigField 'String', 'BUILD_ID', "\"${hgId()}\""
        buildConfigField 'String', 'BUILD_DATE', "\"${buildTime()}\""
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    productFlavors {
    }
}

dependencies {
    api 'androidx.annotation:annotation:1.5.0'
    api 'androidx.appcompat:appcompat:1.5.1'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.gridlayout:gridlayout:1.0.0'
    api 'androidx.legacy:legacy-preference-v14:1.0.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.lifecycle:lifecycle-viewmodel:2.5.1'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    api 'androidx.preference:preference:1.2.0'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'com.google.android.material:material:1.7.0'
}
